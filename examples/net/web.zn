import inject
import io
import json
import net
import web

class IndexHtml {}
class NotFoundHtml {}

def is_index(req) {
  net.parse_request(req).path == '/'
}

@net.HttpRequestController(is_index)
@inject.Inject(keys: IndexHtml)
class IndexController {
  new(field index_html) {}

  @net.Get
  method get(http_request) {
    net.HttpResponse().add_content(index_html)
  }
}

@net.HttpRequestController
@inject.Inject(keys: NotFoundHtml)
class NotFoundController {
  new(field not_found) {}

  @net.Get
  method get(http_request) {
    net.HttpResponse().add_content(not_found)
  }
}

@net.RequestController
class RpcController {
  new() {}

  @net.Rpc
  method process(request) {
    return json.jsonify({'message': 'Hello, world!'})
  }
}

class WebProviders : inject.ProviderSet {
  @inject.Singleton
  @inject.Provides(key: IndexHtml)
  method provide_index() {
    io.FileReader('./examples/sockets/web/index.html').getall()
  }
  @inject.Singleton
  @inject.Provides(key: NotFoundHtml)
  method provide_not_found() {
    io.FileReader('./examples/sockets/web/not_found.html').getall()
  }
}

net.Server()
    .add_application(
        net.Application('0.0.0.0', 80)
            .add_controller(IndexController)
            .add_controller(NotFoundController)
            .add_provider_set(WebProviders))
    .add_application(
        net.Application('0.0.0.0', 1453).add_controller(RpcController))
    .run()